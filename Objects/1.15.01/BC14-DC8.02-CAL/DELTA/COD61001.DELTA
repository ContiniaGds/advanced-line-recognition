OBJECT Codeunit 61001 Adv. Purch/Sales Line Capture
{
  OBJECT-PROPERTIES
  {
    Date=20220222D;
    Time=120000T;
    Modified=true;
    Version List=ALR1.15.01;
  }
  PROPERTIES
  {
    TableNo="CDC Document";
    OnRun=VAR
            CDCTemplate@1000 : Record "CDC Template";
          BEGIN
            //Reset template to standard line capture as we can now use an event
            if not CDCTemplate.Get(Rec."Template No.") then
                exit;

            CDCTemplate."Codeunit ID: Line Capture" := 6085716;
            CDCTemplate.Modify(true);

            CODEUNIT.Run(CDCTemplate."Codeunit ID: Line Capture", Rec);
          END;

  }
  CODE
  {
    VAR
      CaptureMgt@1160040001 : Codeunit "CDC Capture Management";
      LineManagementSI@100000001 : Codeunit "ALR Line Management SI";
      DocumentComment@1000000000 : Record "CDC Document Comment";
      TempMandatoryField@1000000002 : TEMPORARY Record "CDC Temp. Document Field";
      UseBufferWordsEvent@100000000 : Boolean;
      NoMandatoryFieldsFoundLbl@1000 : TextConst 'ENU=You cannot use the advanced line recognition option %1 when the template does not have at least one line field marked as required!';

    [EventSubscriber(Codeunit,6085703,OnAfterFullCapture)]
    [LineStart(10)]
    LOCAL PROCEDURE CDCPurchFullCapture_OnAfterFullCapture@4(VAR Document@1000 : Record "CDC Document");
    BEGIN
      FindAllPONumbersInDocument(Document);
    END;

    [EventSubscriber(Codeunit,6085575,OnBeforeRunLineCaptureCodeunit)]
    [LineStart(13)]
    LOCAL PROCEDURE CDCCaptureEngine_OnBeforeRunLineCaptureCodeunit@1000000000(VAR Document@1000 : Record "CDC Document";VAR Handled@1001 : Boolean);
    VAR
      TempDocLine@1000000001 : TEMPORARY Record "CDC Temp. Document Line";
      TemplateField@1000000000 : Record "CDC Template Field";
      TempSortedDocumentField@1113400000 : TEMPORARY Record "CDC Temp. Document Field";
      CDCTemplate@1002 : Record "CDC Template";
    BEGIN
      CleanupPrevValues(Document);

      if not CDCTemplate.Get(Document."Template No.") then
        exit;

      //RUN STANDARD LINE CAPTURING CODEUNIT
      if (CDCTemplate."Codeunit ID: Line Capture" <> 0) then
        CODEUNIT.Run(CDCTemplate."Codeunit ID: Line Capture", Document);

      OnAfterStandardLineRecognition(Document, Handled);
      if Handled then
        exit;

      //BUILD TEMPORARY LINE TABLE AND LOOP LINES
      Document.BuildTempLinesTable(TempDocLine);

      if TempDocLine.FindSet then begin
        FillSortedFieldBuffer(TempSortedDocumentField, TempMandatoryField, TempDocLine);
        repeat
          TempSortedDocumentField.SetCurrentKey("Document No.","Sort Order");
          if TempSortedDocumentField.FindFirst then
            repeat
              with TemplateField do begin
                Get(TempDocLine."Template No.",TemplateField.Type::Line,TempSortedDocumentField."Field Code");
                case "Advanced Line Recognition Type" of
                  "Advanced Line Recognition Type"::LinkedToAnchorField: FindValueFromOffsetField(TempDocLine,TemplateField);
                  "Advanced Line Recognition Type"::FindFieldByCaptionInPosition: FindValueByCaptionInPosition(TempDocLine,TemplateField,Document);
                  "Advanced Line Recognition Type"::FindFieldByColumnHeading: FindFieldByColumnHeading(TempDocLine,TemplateField,Document);
                end;
              end;
          until TempSortedDocumentField.Next = 0;

          FindSubstitutionFieldValue(TempDocLine);

          GetValueFromPreviousValue(TempDocLine);
        until TempDocLine.Next = 0;

        CleanupTempValues(Document);
      end;
      Handled := true;
    END;

    [LineStart(55)]
    LOCAL PROCEDURE FindValueFromOffsetField@1210050000(TempDocLine@1210050009 : TEMPORARY Record "CDC Temp. Document Line";VAR OffsetField@1000000001 : Record "CDC Template Field");
    VAR
      OffsetSourceField@1113400000 : Record "CDC Template Field";
      OffsetSourceFieldValue@1113400001 : Record "CDC Document Value";
      DocumentValue@1113400002 : Record "CDC Document Value";
      CurrPage@1210050003 : Record "CDC Document Page";
      CurrTop@1000000002 : Integer;
      CurrLeft@1000000003 : Integer;
      CurrBottom@1000000004 : Integer;
      CurrRight@1000000005 : Integer;
    BEGIN
      //This function will capture the field value based on the offset/distance of a source field value

      //Get Line Identification Field Position
      if not OffsetSourceField.Get(TempDocLine."Template No.",OffsetSourceField.Type::Line,OffsetField."Linked Field") then
        exit;

      // Get current value record of offset source field
      if not OffsetSourceFieldValue.Get(TempDocLine."Document No.",true,OffsetSourceField.Code,TempDocLine."Line No.") then
        exit;

      if not CurrPage.Get(TempDocLine."Document No.",OffsetSourceFieldValue."Page No.") then
        exit;

      with OffsetSourceFieldValue do begin
        // Create offset area for value capturing
        CurrTop := Top + OffsetField."Offset Top";
        CurrLeft := Left + OffsetField."Offset Left";
        CurrBottom := CurrTop + OffsetField."Offset Bottom";
        CurrRight := CurrLeft + OffsetField."Offset Right";
        CaptureMgt.CaptureFromPos(CurrPage,OffsetField,TempDocLine."Line No.",true,CurrTop,CurrLeft,CurrBottom,CurrRight,DocumentValue);
        if DocumentValue.Get(TempDocLine."Document No.",true,OffsetField.Code,TempDocLine."Line No.") then
          if ((DocumentValue."Value (Text)" = '') and (DocumentValue."Value (Decimal)" = 0)) or (not DocumentValue."Is Valid" )then
            DocumentValue.Delete;
      end;
    END;

    [LineStart(81)]
    LOCAL PROCEDURE FindValueByCaptionInPosition@1113400000(VAR TempDocLine@1210050009 : TEMPORARY Record "CDC Temp. Document Line";VAR CurrField@1000000001 : Record "CDC Template Field";Document@1000 : Record "CDC Document") : Boolean;
    VAR
      DocumentValue@1113400000 : Record "CDC Document Value";
      DocumentValueCopy@1113400004 : Record "CDC Document Value";
      lCurrPage@1113400009 : Record "CDC Document Page";
      CaptureEngine@1113400002 : Codeunit "CDC Capture Engine";
      Word@1113400003 : Text[1024];
      FromTopPos@1113400008 : Integer;
      FromTopPage@1113400007 : Integer;
      ToBottomPos@1113400006 : Integer;
      ToBottomPage@1113400005 : Integer;
      i@1113400010 : Integer;
    BEGIN
      // We cannot proceed if there is no line field in the template defined as required=true
      if TempMandatoryField.Count = 0 then
        Error(NoMandatoryFieldsFoundLbl, CurrField."Advanced Line Recognition Type");
      //This function will capture the field value based on the caption(s) in the area between the previous and next
      if not Document.Get(TempDocLine."Document No.") then
        exit;

      //Delete current value
      if DocumentValue.Get(TempDocLine."Document No.",true,CurrField.Code,TempDocLine."Line No.") then
        DocumentValue.Delete;

      Clear(Word);

      // Get valid range depended of search direction
      // 1. above the standard recognized line: range is between standard standard line and previous line
      // 2. below the standard recognized line: range is between standard standard line and next line
      if (CurrField."Field value position" = CurrField."Field value position"::AboveStandardLine) then
        GetRangeToPrevLine(Document,TempDocLine,FromTopPage,FromTopPos,ToBottomPage,ToBottomPos)
      else
        GetRangeToNextLine(Document,TempDocLine,FromTopPage,FromTopPos,ToBottomPage,ToBottomPos);

      if (FromTopPage > 0) and (ToBottomPage > 0) then
        for i := FromTopPage to ToBottomPage do begin
          if not lCurrPage.Get(Document."No.",i) then
            exit;

          CurrField."Caption Offset X" := CurrField."ALR Value Caption Offset X";
          CurrField."Caption Offset Y" := CurrField."ALR Value Caption Offset Y";
          CurrField."Typical Field Width" := CurrField."ALR Typical Value Field Width";

          //UseBufferWordsEvent := TRUE;
          if i < ToBottomPage then begin
            LineManagementSI.SetLineRegion(TempDocLine."Document No.",i,FromTopPos,i,lCurrPage."Bottom Word Pos.")
          end else begin
            if (i > FromTopPage) and (i < ToBottomPage) then begin
              LineManagementSI.SetLineRegion(TempDocLine."Document No.",i,0,i,lCurrPage."Bottom Word Pos.")
            end else begin
              if FromTopPos > ToBottomPos then begin
                LineManagementSI.SetLineRegion(TempDocLine."Document No.",i,0,i,ToBottomPos)
              end else begin
                LineManagementSI.SetLineRegion(TempDocLine."Document No.",i,FromTopPos,i,ToBottomPos);
              end;
            end;
          end;

          Word := CaptureEngine.CaptureField(Document,lCurrPage."Page No.",CurrField,false);

          if Word <> '' then begin
            if (DocumentValue.Get(Document."No.",true,CurrField.Code,0)) then begin
              DocumentValueCopy := DocumentValue;
              DocumentValueCopy."Line No." := TempDocLine."Line No.";
              DocumentValueCopy.Type := DocumentValueCopy.Type::Line;
              DocumentValueCopy.Insert;
              DocumentValue.Delete;
            end;
            CaptureMgt.UpdateFieldValue(Document."No.",TempDocLine."Page No.",TempDocLine."Line No.",CurrField,Word,false,false);

            exit(true);
          end;
        end;
    END;

    [LineStart(143)]
    LOCAL PROCEDURE FindFieldByColumnHeading@1113400001(VAR TempDocLine@1210050009 : TEMPORARY Record "CDC Temp. Document Line";VAR CurrField@1000000001 : Record "CDC Template Field";Document@1000 : Record "CDC Document");
    VAR
      Template@1113400021 : Record "CDC Template";
      DocumentValue@1113400000 : Record "CDC Document Value";
      DocumentValueBuffer@1000000002 : TEMPORARY Record "CDC Document Value";
      DocumentValueNew@1113400022 : Record "CDC Document Value";
      CaptionStartWord@1113400004 : ARRAY [100] OF Record "CDC Document Word";
      CaptionEndWord@1113400003 : ARRAY [100] OF Record "CDC Document Word";
      CaptionPage@1113400005 : Record "CDC Document Page";
      CurrPage@1113400017 : Record "CDC Document Page";
      CaptionValue@1113400006 : Record "CDC Document Value";
      CaptionPageNo@1113400001 : Integer;
      CaptionFound@1113400002 : Boolean;
      PageStop@1113400016 : Boolean;
      FromTopPos@1113400010 : Integer;
      FromTopPage@1113400009 : Integer;
      ToBottomPos@1113400008 : Integer;
      ToBottomPage@1113400007 : Integer;
      NewBottom@1113400011 : Integer;
      LineNo@1113400012 : Integer;
      LineHeight@1113400013 : Integer;
      Top@1113400014 : Integer;
      Bottom@1113400015 : Integer;
      Right@1113400018 : Integer;
      FieldLeft@1113400019 : Integer;
      FieldWidth@1113400020 : Integer;
      LastFoundLineNo@1000000000 : Integer;
    BEGIN
      // We cannot proceed if there is no line field in the template defined as required=true
              if TempMandatoryField.Count = 0 then
                  Error(NoMandatoryFieldsFoundLbl, CurrField."Advanced Line Recognition Type");

      //This function will capture the field value based on a column heading, actualy like the default line recognition but filtered on the area between the prev. and next line

      if not Template.Get(TempDocLine."Template No.")then
        exit;

      // Delete old values
      if DocumentValue.Get(TempDocLine."Document No.",true,CurrField.Code,TempDocLine."Line No.") then
        DocumentValue.Delete;

      // Find the Caption position on current or previous pages
      CaptionPageNo := TempDocLine."Page No.";
      repeat
        CaptionFound := GetStartAndEndCaption(CaptionStartWord,CaptionEndWord,CurrField,TempDocLine."Document No.",CaptionPageNo);
        if not CaptionFound then
          CaptionPageNo -= 1;
      until (CaptionPageNo = 0) or CaptionFound;

      if (not CaptionFound) then
        exit;

      if not CaptionPage.Get(TempDocLine."Document No.",CaptionPageNo) then
        exit;

      GetPositionOfCaption(CaptionPage,CurrField,CaptionStartWord[1],CaptionEndWord[1],CaptionValue,FieldLeft,FieldWidth,Bottom,Top);

      // Get position of next or previous line
      if CurrField."Field value position" = CurrField."Field value position"::AboveStandardLine then begin
        GetRangeToPrevLine(Document,TempDocLine,FromTopPage,FromTopPos,ToBottomPage,ToBottomPos);
        if FromTopPos > Top then
          Top := FromTopPos;
      end else begin
        GetRangeToNextLine(Document,TempDocLine,FromTopPage,FromTopPos,ToBottomPage,ToBottomPos);
        Top := FromTopPos;
      end;

      NewBottom := 0;
      LineNo := 1000 * TempDocLine."Page No.";
      LineHeight := 12;


      Bottom := Top + LineHeight;

      PageStop := false;
      if not CurrPage.Get(TempDocLine."Document No.",FromTopPage) then
        exit;

      repeat
        LineNo += 1;
        Right := FieldLeft + FieldWidth;
        NewBottom := CaptureTableCell(Template,Document,CurrPage,CurrField,LineNo,Top,FieldLeft,Bottom,Right);
        if NewBottom > 0 then begin
          if NewBottom > Bottom then
            Bottom := NewBottom;

          if not IsFieldValid(CurrField,Document,LineNo)  then begin
            DocumentValue.Reset;
            DocumentValue.SetRange("Document No.",Document."No.");
            DocumentValue.SetRange("Line No.",LineNo);
            DocumentValue.DeleteAll(true);
          end else begin
            //PageStop := TRUE;
            LastFoundLineNo := LineNo;
            PageStop := (CurrField."Field value search direction" = CurrField."Field value search direction"::Downwards);
            if DocumentValue.Get(TempDocLine."Document No.",true,CurrField.Code,LastFoundLineNo) then begin
              DocumentValueBuffer := DocumentValue;
              DocumentValueBuffer.Insert;
              DocumentValue.Delete;
            end;
          end;
        end;

        if not PageStop then begin
          Top := Bottom;
          Bottom := Top + LineHeight;

          if (Bottom > CurrPage."Bottom Word Pos.") and (CurrPage."Page No." < ToBottomPage) then begin
            //New page - some variables must be reset
            if CurrPage.Get(CurrPage."Document No.",CurrPage."Page No." + 1) then begin
              CaptionPageNo  := CurrPage."Page No.";
              LineNo := 1000 * CurrPage."Page No.";
              if GetStartAndEndCaption(CaptionStartWord,CaptionEndWord,CurrField,TempDocLine."Document No.",CaptionPageNo) then
                if CaptionPage.Get(TempDocLine."Document No.",CurrPage."Page No.") then
                  if GetPositionOfCaption(CaptionPage,CurrField,CaptionStartWord[1],CaptionEndWord[1],CaptionValue,FieldLeft,FieldWidth,Bottom,Top) then
                    Bottom := Top + LineHeight;
            end;
          end else
            if (Bottom > CurrPage."Bottom Word Pos.") or ((Bottom > ToBottomPos) and (CurrPage."Page No." = ToBottomPage)) then
              PageStop := true;
        end;
      until PageStop;

      //Zeilennr. speichern
      if DocumentValueBuffer.Get(TempDocLine."Document No.",true,CurrField.Code,LastFoundLineNo) then begin
        DocumentValueNew := DocumentValueBuffer;
        DocumentValueNew."Line No." := TempDocLine."Line No.";
        DocumentValueNew.Insert;
        DocumentValueBuffer.Delete;
        CaptureMgt.UpdateFieldValue(TempDocLine."Document No.",TempDocLine."Page No.",TempDocLine."Line No.",CurrField,DocumentValueNew."Value (Text)",false,false);
      end;
    END;

    [LineStart(248)]
    LOCAL PROCEDURE FindSubstitutionFieldValue@1113400007(VAR TempDocLine@1000000001 : TEMPORARY Record "CDC Temp. Document Line");
    VAR
      TemplateField@1000000000 : Record "CDC Template Field";
      DocumentValue@1113400002 : Record "CDC Document Value";
      SubstitutionDocumentValue@1000000002 : Record "CDC Document Value";
      SubstitutionField@1113400003 : Record "CDC Template Field";
    BEGIN
      // Function goes through all field, setted up with substitution fields.
      // It checks if the value of the current field is empty and updates the value with the value of the substitution field (if exists).
      TemplateField.SetRange("Template No.",TempDocLine."Template No.");
      TemplateField.SetRange(Type, TemplateField.Type::Line);
      TemplateField.SetFilter("Replacement Field",'<>%1','');
      if TemplateField.IsEmpty then
        exit;

        TemplateField.FindSet;
        repeat
          if not DocumentValue.Get(TempDocLine."Document No.",true,TemplateField.Code,TempDocLine."Line No.") then
            if SubstitutionField.Get(TempDocLine."Template No.",TemplateField."Replacement Field Type",TemplateField."Replacement Field") then
              if SubstitutionDocumentValue.Get(TempDocLine."Document No.",true,SubstitutionField.Code,TempDocLine."Line No.") then begin
                CaptureMgt.UpdateFieldValue(TempDocLine."Document No.",TempDocLine."Page No.",TempDocLine."Line No.",TemplateField,SubstitutionDocumentValue."Value (Text)",false,false);
                if DocumentValue.Get(TempDocLine."Document No.",true,TemplateField.Code,TempDocLine."Line No.") then begin
                  DocumentValue.Top := SubstitutionDocumentValue.Top;
                  DocumentValue.Bottom := SubstitutionDocumentValue.Bottom;
                  DocumentValue.Left := SubstitutionDocumentValue.Left;
                  DocumentValue.Right := SubstitutionDocumentValue.Right;
                  DocumentValue.Modify;
                end;
              end;
        until TemplateField.Next = 0;
    END;

    [LineStart(273)]
    LOCAL PROCEDURE FindAllPONumbersInDocument@100000008(VAR DCDocument@100000000 : Record "CDC Document");
    VAR
      DocumentWord@100000002 : Record "CDC Document Word";
      PurchaseHeader@100000003 : Record "Purchase Header";
      PurchaseHeaderBuffer@100000004 : TEMPORARY Record "Purchase Header";
      Template@1001 : Record "CDC Template";
      TemplateField@100000006 : Record "CDC Template Field";
      DocumentValue@100000008 : Record "CDC Document Value";
      PurchasesPayablesSetup@100000001 : Record "Purchases & Payables Setup";
      NoSeries@100000009 : Record "No. Series";
      NoSeriesLine@100000010 : Record "No. Series Line";
      CDCModuleLicense@1000 : Codeunit "CDC Module License";
      CaptureMgt@100000007 : Codeunit "CDC Capture Management";
      FilterString@100000011 : Text;
      FoundPurchaseOrders@100000005 : Text;
      Pos@100000012 : Integer;
    BEGIN
      if not CDCModuleLicense.IsMatchingActivated(true) then
        exit;

      if not Template.Get(DCDocument."Template No.") then
        exit;

      if not Template."Autom. PO search" then
        exit;

      // Get order no. field first.
      if not TemplateField.Get(DCDocument."Template No.",TemplateField.Type::Header,'OURDOCNO') then
        exit;

      DocumentWord.SetRange("Document No.",DCDocument."No.");

      // Create appropriate Filter string from Purchase Setup >>>
      PurchasesPayablesSetup.Get;
      NoSeries.Get(PurchasesPayablesSetup."Order Nos.");
      NoSeriesLine.SetRange("Series Code",NoSeries.Code);
      NoSeriesLine.SetFilter("Starting Date",'%1|<=%2',0D,Today);
      NoSeriesLine.SetRange(Open,true);
      if NoSeriesLine.FindFirst then begin
        Pos := 1;

        while (Pos <= StrLen(NoSeriesLine."Starting No.")) do begin
          if NoSeriesLine."Starting No."[Pos] in ['0'..'9'] then
            FilterString += '?'
          else
            FilterString += Format(NoSeriesLine."Starting No."[Pos]);
          Pos += 1;
        end;
      end;

      if StrLen(FilterString) = 0 then
        exit;
      // Iterate through Document Word table and filter for our PO number filter string
      DocumentWord.SetFilter(Word,FilterString);
      if DocumentWord.FindSet then
        repeat
          // Check if there is a PO in the system with the matched word
          if PurchaseHeader.Get(PurchaseHeader."Document Type"::Order,CopyStr(UpperCase(DocumentWord.Word),1,MaxStrLen(PurchaseHeader."No."))) then begin
            // Check if the number exists in the temp. PO buffer
            if not PurchaseHeaderBuffer.Get(PurchaseHeader."Document Type",PurchaseHeader."No.") then begin
              PurchaseHeaderBuffer := PurchaseHeader;
              PurchaseHeaderBuffer.Insert;
            end;
          end;
        until DocumentWord.Next=0;

      // Iterate through all found PO's and create the string, that can be used for order matching
      if PurchaseHeaderBuffer.FindFirst then
        repeat
          if (StrLen(FoundPurchaseOrders) + StrLen(PurchaseHeaderBuffer."No.") + 1) <= MaxStrLen(DocumentValue."Value (Text)") then begin
            if (StrLen(FoundPurchaseOrders) > 0) then
                FoundPurchaseOrders += ',';
              FoundPurchaseOrders += PurchaseHeaderBuffer."No.";
          end;
        until PurchaseHeaderBuffer.Next = 0;

        TemplateField.Get(DCDocument."Template No.",TemplateField.Type::Header,'OURDOCNO');
        CaptureMgt.UpdateFieldValue(DCDocument."No.",0,0,TemplateField,FoundPurchaseOrders,false,false);
    END;

    [LineStart(336)]
    LOCAL PROCEDURE GetValueFromPreviousValue@1000000004(VAR TempDocLine@1000000001 : TEMPORARY Record "CDC Temp. Document Line");
    VAR
      TemplateField@1000000000 : Record "CDC Template Field";
      DocumentValue@1113400002 : Record "CDC Document Value";
    BEGIN
      // Function goes through all field, setted up with substitution fields.
      // It checks if the value of the current field is empty and updates the value with the value of the substitution field (if exists).
      TemplateField.SetRange("Template No.",TempDocLine."Template No.");
      TemplateField.SetRange("Copy Value from Previous Value",true);
      if TemplateField.FindSet then
        repeat
          if not DocumentValue.Get(TempDocLine."Document No.",true,TemplateField.Code,TempDocLine."Line No.") then
            if DocumentValue.Get(TempDocLine."Document No.",true,TemplateField.Code,TempDocLine."Line No."-1) then
              CaptureMgt.UpdateFieldValue(TempDocLine."Document No.",TempDocLine."Page No.",TempDocLine."Line No.",TemplateField,DocumentValue."Value (Text)",false,false);
        until TemplateField.Next = 0;
    END;

    [LineStart(348)]
    LOCAL PROCEDURE GetRangeToNextLine@1113400002(Document@1000 : Record "CDC Document";VAR TempDocLine@1000000000 : Record "CDC Temp. Document Line";VAR SearchFromPage@1113400002 : Integer;VAR SearchFromPos@1113400003 : Integer;VAR SearchToPage@1113400000 : Integer;VAR SearchToPos@1113400001 : Integer);
    VAR
      DocumentValue@1000000001 : Record "CDC Document Value";
      CurrPage@1113400004 : Record "CDC Document Page";
      StopPos@1000000002 : ARRAY [100] OF Integer;
    BEGIN
      // This function calculates the range until the next position/line
      Clear(SearchFromPage);
      Clear(SearchFromPos);
      Clear(SearchToPage);
      Clear(SearchToPos);


      DocumentValue.SetCurrentKey("Document No.","Is Value",Code,"Line No.");
      DocumentValue.SetRange("Document No.",TempDocLine."Document No.");
      DocumentValue.SetRange("Is Value",true);
      DocumentValue.SetRange(Type,DocumentValue.Type::Line);
      DocumentValue.SetFilter("Page No.", '>%1',0);

      GetCurrLinePosition(DocumentValue,TempDocLine."Line No.",SearchFromPage, SearchFromPos, SearchToPage, SearchToPos);

      // Filter for next line
      DocumentValue.SetRange("Line No.",TempDocLine."Line No." + 1);
      if DocumentValue.FindSet then begin
        repeat
          if (SearchToPage < DocumentValue."Page No.") or (SearchToPage = 0)  then begin
            SearchToPage := DocumentValue."Page No.";
            SearchToPos := 0;
          end;

          if SearchToPage = DocumentValue."Page No." then begin
            if (SearchToPos < DocumentValue.Bottom) or (SearchToPos = 0) then
                SearchToPos := DocumentValue.Bottom ;
          end;
        until DocumentValue.Next = 0;
      end else begin
        // As there is no next line, calculate to next header value or bottom of current page
        DocumentValue.SetCurrentKey("Document No.","Is Value",Code,"Line No.");
        DocumentValue.SetRange("Document No.",TempDocLine."Document No.");
        DocumentValue.SetRange("Is Value",false);
        DocumentValue.SetRange(Type,DocumentValue.Type::Header);
        DocumentValue.SetRange("Page No.",SearchToPage);
        DocumentValue.SetFilter(Top,'>%1',SearchToPos);
        DocumentValue.SetRange("Line No.", 0);
        if DocumentValue.FindSet(false,false) then begin
          if DocumentValue."Page No." > SearchToPage then begin
            SearchToPage := DocumentValue."Page No.";
          end;
          SearchToPos := DocumentValue.Top
        end else begin
          DocumentValue.SetFilter("Page No.",'>%1',SearchToPage);
          DocumentValue.SetRange(Top);
          if DocumentValue.FindSet(false,false) then begin
            if DocumentValue."Page No." > SearchToPage then begin
              SearchToPage := DocumentValue."Page No.";
            end;
            SearchToPos := DocumentValue.Top
          end else
            if CurrPage.Get(TempDocLine."Document No.",SearchToPage) then
              SearchToPos := CurrPage."Bottom Word Pos.";
        end;
      end;

      GetStopLineRecognitionPositions(StopPos,SearchToPage,SearchToPos,Document);
      if (StopPos[SearchToPage]>0) and (StopPos[SearchToPage] <= SearchToPos) then
        SearchToPos := StopPos[SearchToPage];
    END;

    [LineStart(410)]
    LOCAL PROCEDURE GetRangeToPrevLine@1113400009(Document@1000 : Record "CDC Document";VAR TempDocLine@1000000000 : Record "CDC Temp. Document Line";VAR RangeTopPage@1113400002 : Integer;VAR RangeTopPos@1113400003 : Integer;VAR RangeBottomPage@1113400000 : Integer;VAR RangeBottomPos@1113400001 : Integer);
    VAR
      DocumentValue@1000000001 : Record "CDC Document Value";
      CurrLineTopPage@1113400008 : Integer;
      CurrLineTopPos@1113400007 : Integer;
      CurrLineBottomPage@1113400006 : Integer;
      CurrLineBottomPos@1113400005 : Integer;
      PrevLineTopPage@1113400012 : Integer;
      PrevLineTopPos@1113400011 : Integer;
      PrevLineBottomPage@1113400010 : Integer;
      PrevLineBottomPos@1113400009 : Integer;
    BEGIN
      // This function calculates the range until the previous position/line
      Clear(PrevLineTopPage);
      Clear(PrevLineTopPos);
      Clear(PrevLineBottomPage);
      Clear(PrevLineBottomPos);

      with DocumentValue do begin
        SetCurrentKey("Document No.","Is Value",Code,"Line No.");
        SetRange("Document No.",Document."No.");
        SetRange("Is Value",true);
        SetRange(Type,Type::Line);
        SetFilter("Page No.", '>%1',0);

        GetCurrLinePosition(DocumentValue,TempDocLine."Line No.",CurrLineTopPage,CurrLineTopPos,CurrLineBottomPage,CurrLineBottomPos);

        // Filter for Prev line
        SetRange("Line No.",TempDocLine."Line No." - 1);
        if FindSet then begin
          repeat
            if ("Page No." < PrevLineTopPage) or (PrevLineTopPage = 0) then begin
              PrevLineTopPage := "Page No.";
              Clear(PrevLineTopPos);
            end;

            if ("Page No." > PrevLineBottomPage) or (PrevLineBottomPage = 0)  then begin
              PrevLineBottomPage := "Page No.";
              Clear(PrevLineBottomPos);
            end;

            if PrevLineTopPage = "Page No." then
              if (Top < PrevLineTopPos) or (PrevLineTopPos = 0) then
                PrevLineTopPos := Top;

            if PrevLineBottomPage = "Page No." then
              if (Bottom > PrevLineBottomPos) or (PrevLineBottomPos = 0) then
                 PrevLineBottomPos := Bottom ;
          until Next = 0;
        end else begin
          // As there is no Prev line, calculate to Prev header value or bottom of current page
          SetCurrentKey("Document No.","Is Value",Code,"Line No.");
          SetRange("Document No.",Document."No.");
          SetRange("Is Value",false);
          SetRange(Type,Type::Header);
          SetFilter("Page No.",'<=%1',"Page No.");
          SetFilter(Top,'<%1',CurrLineTopPos);
          if FindSet(false,false) then begin
            PrevLineBottomPos := Bottom;
            PrevLineBottomPage := "Page No.";
          end else begin
            PrevLineBottomPos  := 0;
            PrevLineBottomPage := CurrLineTopPage;
          end;
        end;

        RangeTopPage := PrevLineBottomPage;
        RangeBottomPage := CurrLineTopPage;
        RangeTopPos := PrevLineBottomPos + 1;
        //RangeBottomPos := CurrLineTopPos - 1;
        RangeBottomPos := CurrLineBottomPos - 1;
      end;
    END;

    [LineStart(472)]
    LOCAL PROCEDURE GetCurrLinePosition@1000000002(VAR DocumentValue@1000000000 : Record "CDC Document Value";LineNo@1000000001 : Integer;VAR CurrLineTopPage@1000000005 : Integer;VAR CurrLineTopPos@1000000004 : Integer;VAR CurrLineBottomPage@1000000003 : Integer;VAR CurrLineBottomPos@1000000002 : Integer);
    BEGIN
      with DocumentValue do begin
        // Filter for current line
        SetRange("Line No.",LineNo);
        if FindSet then
        repeat
          if TempMandatoryField.Get(DocumentValue.GetFilter("Document No."),Code) then begin
            if ("Page No." < CurrLineTopPage) or (CurrLineTopPage = 0) then begin
              CurrLineTopPage := "Page No.";
              Clear(CurrLineTopPos);
            end;

            if ("Page No." > CurrLineBottomPage) or (CurrLineBottomPage = 0)  then begin
              CurrLineBottomPage := "Page No.";
              Clear(CurrLineBottomPos);
            end;

            if CurrLineTopPage = "Page No." then
              if (Top < CurrLineTopPos) or (CurrLineTopPos = 0) then
                CurrLineTopPos := Top;

            if CurrLineBottomPage = "Page No." then
              if (Bottom > CurrLineBottomPos) or (CurrLineBottomPos = 0) then
                CurrLineBottomPos := Bottom ;
          end;
        until Next = 0;
      end;
    END;

    [LineStart(500)]
    LOCAL PROCEDURE GetStartAndEndCaption@1160040001(VAR CaptionStartWord@1160040002 : ARRAY [100] OF TEMPORARY Record "CDC Document Word";VAR CaptionEndWord@1160040001 : ARRAY [100] OF TEMPORARY Record "CDC Document Word";Field@1160040000 : Record "CDC Template Field";DocNo@1160040003 : Code[20];PageNo@1160040006 : Integer) : Boolean;
    VAR
      TemplateFieldCaption@1160040005 : Record "CDC Template Field Caption";
      CaptureEngine@1160040004 : Codeunit "CDC Capture Engine";
      PrevCaptionStartWord@1113400000 : Record "CDC Document Word";
    BEGIN
      Clear(CaptionStartWord);
      Clear(CaptionEndWord);

      TemplateFieldCaption.SetRange("Template No.",Field."Template No.");
      TemplateFieldCaption.SetRange(Type,Field.Type);
      TemplateFieldCaption.SetRange(Code,Field.Code);
      if TemplateFieldCaption.FindSet then
        repeat
          if CaptureEngine.FindCaption(DocNo,PageNo,Field,TemplateFieldCaption,CaptionStartWord,CaptionEndWord) then
            exit(true);
        until (TemplateFieldCaption.Next = 0) or ((CaptionStartWord[1].Word <> '') and (CaptionEndWord[1].Word <> ''));
    END;

    [LineStart(513)]
    LOCAL PROCEDURE GetPositionOfCaption@1113400003(CurrPage@1113400004 : Record "CDC Document Page";CaptionTemplateField@1113400001 : Record "CDC Template Field";CaptionStartWord@1113400003 : Record "CDC Document Word";CaptionEndWord@1113400002 : Record "CDC Document Word";DocumentValue@1113400000 : Record "CDC Document Value";VAR FieldLeft@1113400011 : Integer;VAR FieldWidth@1113400010 : Integer;VAR Bottom@1113400009 : Integer;VAR Top@1113400008 : Integer) CaptionValueFound : Boolean;
    VAR
      Template@1113400007 : Record "CDC Template";
      CaptureEngine@1113400006 : Codeunit "CDC Capture Engine";
    BEGIN
      Template.Get(CaptionTemplateField."Template No.");

      //Hole Positionen der caption
        CaptionValueFound := CaptureMgt.CaptureFromPos(CurrPage,CaptionTemplateField,0,false,CaptionStartWord.Top,CaptionStartWord.Left,
          CaptionEndWord.Bottom,CaptionEndWord.Right,DocumentValue) <> '';

        if CaptionValueFound then begin
          FieldLeft := CaptionStartWord.Left +
          Round(CaptionTemplateField."Caption Offset X" * CaptureEngine.GetDPIFactor(CaptionTemplateField."Offset DPI",CurrPage."TIFF Image Resolution"),1);

          if not Template."First Table Line Has Captions" then
            Bottom := CaptionStartWord.Top
          else if CaptionStartWord.Bottom > Bottom then
            Bottom := CaptionStartWord.Bottom;

          if FieldWidth < CaptionEndWord.Right - CaptionStartWord.Left then
            FieldWidth := CaptionEndWord.Right - CaptionStartWord.Left;

          Top := CaptionStartWord.Top;
        end;
    END;

    [LineStart(535)]
    LOCAL PROCEDURE CaptureTableCell@1000000005(VAR Template@161024014 : Record "CDC Template";VAR Document@1000000009 : Record "CDC Document";VAR Page@1000000000 : Record "CDC Document Page";VAR Field@1000000010 : Record "CDC Template Field";LineNo@1000000003 : Integer;Top@1000000007 : Integer;Left@1000000006 : Integer;Bottom@1000000005 : Integer;Right@1000000004 : Integer) : Integer;
    VAR
      Value@1000000008 : Record "CDC Document Value";
    BEGIN
      if (Right - Left <= 0) or (Bottom-Top <= 0) then
        exit;

      CaptureMgt.CaptureFromPos(Page,Field,LineNo,true,Top,Left,Bottom,Right,Value);
      Value.Find('=');

      if (Value.IsBlank) or TableCellAlreadyCaptured(Template,Page,Value) then
        Value.Delete
      else
        exit(Value.Bottom);
    END;

    [LineStart(547)]
    LOCAL PROCEDURE TableCellAlreadyCaptured@1000000007(VAR Template@161024012 : Record "CDC Template";VAR Page@1000000002 : Record "CDC Document Page";VAR Value@1000000000 : Record "CDC Document Value") : Boolean;
    VAR
      Value2@1000000001 : Record "CDC Document Value";
      CaptureEngine@1113400000 : Codeunit "CDC Capture Engine";
    BEGIN
      Value2.SetCurrentKey("Document No.","Is Value",Type,"Page No.");
      if not Template."First Table Line Has Captions" then
        Value2.SetRange("Is Value",true);
      Value2.SetRange("Document No.",Page."Document No.");
      Value2.SetRange(Type,Value2.Type::Line);
      Value2.SetRange("Page No.",Value."Page No.");

      Value.Top := Value.Top + Round((Value.Bottom - Value.Top) / 2,1);
      Value.Left := Value.Left + 3;

      if Value2.FindSet(false,false) then
        repeat
          if (not ((Value2.Code = Value.Code) and (Value2."Line No." = Value."Line No."))) then
            if CaptureEngine.IntersectsWith(Value,Value2) then
              exit(true);
        until Value2.Next = 0;
    END;

    [LineStart(565)]
    LOCAL PROCEDURE IsFieldValid@1113400005(VAR CaptionField@1113400000 : Record "CDC Template Field";Document@1000000001 : Record "CDC Document";LineNo@1000000002 : Integer) : Boolean;
    VAR
      Field@1000000000 : Record "CDC Template Field";
      Value@161024012 : Record "CDC Document Value";
    BEGIN
      case CaptionField."Data Type" of
        Field."Data Type"::Number:
          begin
            if (not CaptionField.Required) then begin
              if Value.Get(Document."No.",true,CaptionField.Code,LineNo) then begin
                if not Value."Is Valid" then begin
                  exit;
                end else begin
                  exit(CaptureMgt.ParseNumber(Field,Value."Value (Text)",Value."Value (Decimal)"));
                end;
              end;
            end;
          end;
        Field."Data Type"::Text:
          if Value.Get(Document."No.",true,CaptionField.Code,LineNo) then begin
            exit(IsValidText(CaptionField, Value."Value (Text)",Document."No."));
          end;
        Field."Data Type"::Date:
          if Value.Get(Document."No.",true,CaptionField.Code,LineNo) then
            exit(IsValidDate(CaptionField, Value."Value (Date)"));
        Field."Data Type"::Lookup:
          if Value.Get(Document."No.",true,CaptionField.Code,LineNo) then
            exit(IsValidLookup(CaptionField, Value."Value (Text)",Document."No."));
      end;
      exit(true);
    END;

    [LineStart(592)]
    LOCAL PROCEDURE FillSortedFieldBuffer@1113400004(VAR TempSortedDocumentField@1113400000 : Record "CDC Temp. Document Field";VAR MandatoryField@1000000000 : Record "CDC Temp. Document Field";TempDocLine@1113400003 : TEMPORARY Record "CDC Temp. Document Line");
    VAR
      TemplateField@1113400001 : Record "CDC Template Field";
    BEGIN
      with TemplateField do begin
        SetRange("Template No.",TempDocLine."Template No.");
        SetRange(Type,Type::Line);
        if FindSet then
          repeat
            if ("Advanced Line Recognition Type" <> "Advanced Line Recognition Type"::Default) and
               (StrLen(Formula)=0) and (StrLen(GetFixedValue)=0)  then
            begin
              TempSortedDocumentField."Document No." := TempDocLine."Document No.";
              TempSortedDocumentField."Sort Order" := Sorting;
              TempSortedDocumentField."Field Code" := Code;
              TempSortedDocumentField.Insert;
            end else begin
              if Required then begin
                MandatoryField."Document No." := TempDocLine."Document No.";
                MandatoryField."Sort Order" := Sorting;
                MandatoryField."Field Code" := Code;
                MandatoryField.Insert;
              end;
            end;
          until Next = 0;
      end;
    END;

    [LineStart(616)]
    LOCAL PROCEDURE GetStopLineRecognitionPositions@1000000001(VAR StopPos@1000000003 : ARRAY [100] OF Integer;CurrPageNo@1000000002 : Integer;Bottom@1000000004 : Integer;Document@1000 : Record "CDC Document");
    VAR
      Field@1000000000 : Record "CDC Template Field";
      Value@1000000001 : Record "CDC Document Value";
    BEGIN
      Field.Reset;
      Field.SetCurrentKey("Template No.",Type,"Sort Order");
      Field.SetRange("Template No.",Document."Template No.");
      Field.SetRange(Type,Field.Type::Header);
      Field.SetFilter("Stop Lines Recognition",'>%1',Field."Stop Lines Recognition"::" ");
      if Field.FindSet then
        repeat
          Value.Reset;
          Value.SetRange("Document No.",Document."No.");
          Value.SetRange(Type,Field.Type);
          Value.SetRange(Code,Field.Code);
          Value.SetRange("Page No.",CurrPageNo);

          case Field."Stop Lines Recognition" of
            Field."Stop Lines Recognition"::"If Caption is on same line",
            Field."Stop Lines Recognition"::"If Caption is on same line (continue on next page)":
              Value.SetRange("Is Value",false);
            Field."Stop Lines Recognition"::"If Value is on same line",
            Field."Stop Lines Recognition"::"If Value is on same line (continue on next page)":
              Value.SetRange("Is Value",true);
            Field."Stop Lines Recognition"::"If Caption or Value is on same line",
            Field."Stop Lines Recognition"::"If Caption or Value is on same line (continue on next page)":
              Value.SetRange("Is Value");
          end;

          Value.SetFilter(Top,'>%1',0);
          if Value.FindFirst then begin
            if (StopPos[Value."Page No."] = 0) or (StopPos[Value."Page No."] > Value.Top) then
              if (not (Value.Left = Value.Right) and (Value.Bottom = Value.Top)) then
                StopPos[Value."Page No."] := Value.Top;
          end;
        until Field.Next = 0;
    END;

    [LineStart(650)]
    LOCAL PROCEDURE CleanupPrevValues@100000000(Document@1000 : Record "CDC Document");
    VAR
      DocumentValue@100000000 : Record "CDC Document Value";
    BEGIN
      DocumentValue.SetRange("Document No.",Document."No.");
      DocumentValue.SetRange(Type,DocumentValue.Type::Line);
      DocumentValue.DeleteAll;
    END;

    [LineStart(655)]
    LOCAL PROCEDURE CleanupTempValues@1160040000(Document@1000 : Record "CDC Document");
    VAR
      DocumentValue@1160040000 : Record "CDC Document Value";
    BEGIN
      // Clean up temporary created values
      DocumentValue.SetRange("Document No.",Document."No.");
      DocumentValue.SetRange(Type,DocumentValue.Type::Line);
      DocumentValue.SetFilter("Line No.",'1000..');
      DocumentValue.DeleteAll;
    END;

    [LineStart(662)]
    LOCAL PROCEDURE "--CopiesFromCaptureManagement--"@1160040009();
    BEGIN
      // The following functions are a copy of the same functions in the Capture Management Codeunit, where they are defined as "local"
    END;

    [LineStart(665)]
    LOCAL PROCEDURE IsValidNumber@6085574(VAR Field@6085575 : Record "CDC Template Field";Number@6085574 : Decimal) : Boolean;
    VAR
      FieldRule@6085576 : Record "CDC Template Field Rule";
      TempTemplateField@6085573 : TEMPORARY Record "CDC Template Field";
    BEGIN
      if Field."Codeunit ID: Capture Value" <> 0 then
        exit(TestCaptureValue(Field,FieldRule,Format(Number)));

      if (Number = 0) and Field.Required then
        exit(false);

      CaptureMgt.FilterRule(Field,FieldRule);

      TempTemplateField."Fixed Value (Decimal)" := Number;
      TempTemplateField.Insert;

      if FieldRule.FindSet then
        repeat
          if FieldRule.Rule <> '' then begin
            TempTemplateField.SetFilter("Fixed Value (Decimal)",FieldRule.Rule);
            if TempTemplateField.IsEmpty then
              exit(false);
          end;
        until FieldRule.Next = 0;

      exit(true);
    END;

    [LineStart(688)]
    LOCAL PROCEDURE IsValidDate@1000000014(VAR Field@1000000001 : Record "CDC Template Field";Date@1000000000 : Date) : Boolean;
    VAR
      FieldRule@161024012 : Record "CDC Template Field Rule";
    BEGIN
      if Field."Codeunit ID: Capture Value" <> 0 then
        exit(TestCaptureValue(Field,FieldRule,Format(Date)));

      if (Date = 0D) then
        exit(not Field.Required);

      if Format(Field."Validation Dateformula From") <> '' then
        if Date < CalcDate(Field."Validation Dateformula From",Today) then
          exit(false);

      if Format(Field."Validation Dateformula To") <> '' then
        if Date > CalcDate(Field."Validation Dateformula To",Today) then
          exit(false);

      if Date < 17540101D then
        exit(false);

      exit(true);
    END;

    [LineStart(708)]
    LOCAL PROCEDURE IsValidText@1000000015(VAR Field@1000000001 : Record "CDC Template Field";Text@1000000000 : Text[250];DocumentNo@6085573 : Code[20]) : Boolean;
    VAR
      FieldRule@1000000004 : Record "CDC Template Field Rule";
      TempValue@161024012 : TEMPORARY Record "CDC Document Value";
      RegEx@1000000003 : Codeunit "CDC RegEx Management";
      IsValid@1000000002 : Boolean;
    BEGIN
      Text := UpperCase(Text);

      if Field."Codeunit ID: Capture Value" <> 0 then
        if TestCaptureValue(Field,FieldRule,Text) then
          exit(true);

      CaptureMgt.FilterRule(Field,FieldRule);
      if not FieldRule.FindFirst then
        exit((Text <> '') or (not Field.Required));

      if Text = '' then
        exit(not Field.Required);

      TempValue."Value (Text)" := Text;
      TempValue.Insert;

      repeat
        FieldRule.Rule := UpperCase(FieldRule.Rule);
        if (StrPos(FieldRule.Rule,'<') <> 0) or
          (StrPos(FieldRule.Rule,'>') <> 0) or
          (StrPos(FieldRule.Rule,'|') <> 0) or
          (StrPos(FieldRule.Rule,'*') <> 0) or
          (StrPos(FieldRule.Rule,'&') <> 0)
        then begin
          TempValue.SetFilter("Value (Text)",FieldRule.Rule);
          IsValid := not TempValue.IsEmpty;
        end else begin
          if RegEx.IsMatch(Text,FieldRule.Rule) then begin
            if Field."Codeunit ID: Capture Value" <> 0 then
              IsValid := TestCaptureValue(Field,FieldRule,Text)
            else
              IsValid := true;

            if IsValid and Field."Enable Rule Generation" then begin
              ClearFldRuleCreatedFromMaster(FieldRule,DocumentNo);

              // Several rules could have been copied from the master template. Delete these when a rule matches the found value
              DelFldRulesCreatedFromMaster(FieldRule."Entry No.");
            end;
          end;
        end;
      until (FieldRule.Next = 0) or (IsValid);

      exit(IsValid);
    END;

    [LineStart(754)]
    LOCAL PROCEDURE IsValidLookup@1000000031(Field@1000000001 : Record "CDC Template Field";Value@1160040000 : Text[250];DocumentNo@6085573 : Code[20]) : Boolean;
    VAR
      RecIDMgt@1160040001 : Codeunit "CDC Record ID Mgt.";
    BEGIN
      exit(IsValidText(Field,Value,DocumentNo));
    END;

    [LineStart(757)]
    LOCAL PROCEDURE IsValidBoolean@1160040016(VAR Field@1000000001 : Record "CDC Template Field";Boolean@1000000000 : Boolean) : Boolean;
    BEGIN
      exit(true);
    END;

    [LineStart(760)]
    LOCAL PROCEDURE TestCaptureValue@161024017(Field@161024012 : Record "CDC Template Field";Rule@161024013 : Record "CDC Template Field Rule";Value@161024014 : Text[1024]) : Boolean;
    VAR
      TempCaptureFieldVal@161024015 : Record "CDC Temp. Capture Field Valid.";
    BEGIN
      TempCaptureFieldVal."Field Type" := Field.Type;
      TempCaptureFieldVal."Field Code" := Field.Code;
      TempCaptureFieldVal."File Rule Entry No." := Rule."Entry No.";
      TempCaptureFieldVal.Rule := Rule.Rule;
      TempCaptureFieldVal.Value := Value;
      CODEUNIT.Run(Field."Codeunit ID: Capture Value",TempCaptureFieldVal);
      exit(TempCaptureFieldVal."Is Valid");
    END;

    [LineStart(769)]
    LOCAL PROCEDURE ClearFldRuleCreatedFromMaster@6085577(VAR FieldRule@6085573 : Record "CDC Template Field Rule";DocumentNo@6085574 : Code[20]);
    BEGIN
      if FieldRule."Created from Master Template" then begin
        FieldRule."Created from Master Template" := false;
        FieldRule."Document No." := DocumentNo;
        FieldRule.Modify(true);
      end;
    END;

    [LineStart(776)]
    LOCAL PROCEDURE DelFldRulesCreatedFromMaster@6085579(SkipEntryNo@6085574 : Integer);
    VAR
      FieldRule@6085573 : Record "CDC Template Field Rule";
    BEGIN
      FieldRule.SetRange("Created from Master Template",true);
      FieldRule.SetFilter("Entry No.",'<>%1',SkipEntryNo);
      if not FieldRule.IsEmpty then
        FieldRule.DeleteAll(true);
    END;

    [Integration(TRUE)]
    [LineStart(782)]
    LOCAL PROCEDURE OnAfterStandardLineRecognition@1160040002(VAR Document@1160040000 : Record "CDC Document";VAR Handled@1160040001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

